import java.time.LocalDateTime

allprojects {
    group APP_GROUP
    version APP_VERSION
}

ext {
    configuration = [
            javaVersion = JavaVersion.VERSION_21
    ]

    libraries = [
            springBootVersion = '3.2.2',
            springVersion = '5.3.31',
            hutoolVersion = '5.8.25'
    ]

    lib = [
            "slf4j-api"           : "org.slf4j:slf4j-api:1.7.36",
            "fastjson2"           : "com.alibaba.fastjson2:fastjson2:2.0.42",
            "logback-classic"     : "ch.qos.logback:logback-classic:1.4.14",
            "hutool-all"          : "cn.hutool:hutool-all:${hutoolVersion}",
            "guava"               : "com.google.guava:guava:33.0.0-jre",
    ]
}

description = "Simplifying mq usage"

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'
    apply plugin: "io.freefair.lombok"
    apply plugin: 'com.github.hierynomus.license'

    sourceCompatibility = "${javaVersion}"
    targetCompatibility = "${javaVersion}"

    lombok {
        version = "1.18.30"
    }

    compileJava {
        options.release = 17
    }

    repositories {
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        mavenCentral()
    }

    def onlyPom = project.name == "hala-bom" || project.name.contains("spring-boot-starter")

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.warnings = false
        options.deprecation = true
        options.compilerArgs += ["-parameters"]
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    jar {
        into("META-INF/") {
            from rootProject.file("LICENSE")
        }
        into("META-INF/maven/$project.group/$project.name") {
            from { generatePomFileForMavenJavaPublication }
            rename ".*", "pom.xml"
        }
        afterEvaluate {
            manifest {
                attributes 'Implementation-Title': archiveBaseName
                attributes 'Implementation-Version': archiveVersion
                attributes 'Built-Gradle': gradle.gradleVersion
                attributes 'Bundle-DocURL': 'https://helaframework.github.io/hela/'
                attributes 'Build-OS': System.getProperty("os.name")
                attributes 'Built-By': System.getProperty("user.name")
                attributes 'Build-Jdk': System.getProperty("java.version")
                attributes 'Build-Timestamp': LocalDateTime.now().format("yyyy-MM-dd HH:mm:ss")
                attributes 'Automatic-Module-Name': "${project.group}.${project.name.replaceAll("-", ".")}"
            }
        }
    }

    license {
        encoding = "UTF-8"
        header = rootProject.file("header.txt")
        includes(["**/*.java", "**/*.kt"])
        mapping {
            java = "SLASHSTAR_STYLE"
            kt = "SLASHSTAR_STYLE"
        }
        ignoreFailures = true
        ext.year = Calendar.getInstance().get(Calendar.YEAR)
    }

    tasks.register('sourcesJar', Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    javadoc {
        // Check for syntax during linting. 'none' doesn't seem to work in suppressing
        // all linting warnings all the time (see/link references most notably).
        options.addStringOption("Xdoclint:syntax", "-quiet")

        // Suppress warnings due to cross-module @see and @link references.
        // Note that global 'api' task does display all warnings, and
        // checks for 'reference' on top of 'syntax'.
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message

        afterEvaluate {
            configure(options) {
                encoding "UTF-8"
                charSet 'UTF-8'
                author true
                version true
                failOnError false
                links "https://docs.oracle.com/javase/8/docs/api"
            }
        }
    }

    test {
        dependsOn("cleanTest", "generatePomFileForMavenJavaPublication")
        useJUnitPlatform()
    }

    tasks.register('javadocJar', Jar) {
        archiveClassifier = 'javadoc'
        from javadoc
    }

    tasks.configureEach { task ->
        if (task.name.contains('signMavenJavaPublication') && project.hasProperty('signing.secretKeyRingFile')) {
            task.enabled = new File(project.property('signing.secretKeyRingFile') as String).isFile()
        }
    }

    publishing {
        repositories {
            maven {
                def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    if (project.hasProperty('storedUsername') && project.hasProperty('storedPassword')) {
                        username project.storedUsername
                        password project.storedPassword
                    }
                }
            }
        }
        publications {
            mavenJava(MavenPublication) {
                if(!onlyPom) {
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar
                }
                pom {
                    name = 'hela'
                    if(onlyPom) {
                        packaging 'pom'
                    } else {
                        packaging 'jar'
                    }
                    description = 'Simplified workflow with .'
                    url = 'https://github.com/helaframework/hela'

                    scm {
                        connection = 'scm:git@github.com:helaframework/hela.git'
                        developerConnection = 'scm:git@github.com:helaframework/hela.git'
                        url = 'https://github.com/helaframework/hela'
                    }

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'fubluesky'
                            name = 'yanghq'
                            email = 'fubluesky@foxmail.com'
                        }
                    }

                    withXml {
                        def root = asNode()
                        root.dependencies.'*'.findAll {
                            def d = it
                            d.scope.text() == 'runtime' && project.configurations.named("implementation").allDependencies.find { dep ->
                                dep.name == it.artifactId.text()
                            }.each() {
                                d.scope*.value = 'compile'
                                d.appendNode('optional', true)
                            }
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.mavenJava
        }
    }
}
